ext.projectVersion = '2.0.0'
ext.projectLocation = "https://github.com/iamthechad/javadoc2dash"

apply plugin: 'com.github.kt3k.coveralls'

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:+'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:+'
        classpath "com.gradle.publish:plugin-publish-plugin:1.2.0"
        classpath "com.github.spotbugs.snom:spotbugs-gradle-plugin:5.0.14"
        classpath "name.remal:gradle-plugins:1.8.4"
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'name.remal.check-updates'

    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    jacoco.toolVersion = "0.8.7" // Java 17 support
}

subprojects {
    sourceCompatibility = 1.8
    version = "$projectVersion"
    group = "com.megatome.doc2dash"

    def javaApiUrl = 'https://docs.oracle.com/javase/8/docs/api/'
    def groovyApiUrl = 'http://groovy-lang.org/gapi/'
    tasks.withType(Javadoc) {
        options.links(javaApiUrl, groovyApiUrl)
    }

    jacocoTestReport {
        reports {
            xml.required = true // coveralls plugin depends on xml format report
            html.required = true
        }
    }

    apply plugin: "com.github.spotbugs"
    spotbugsMain {
        sourceDirs = files( sourceSets.main.allSource.srcDirs )
        classDirs = sourceSets.main.output
        auxClassPaths = sourceSets.main.compileClasspath
        reports {
            xml.required = false
            html.required = true
        }
    }
}

def publishedProjects = subprojects.findAll {
    !it.path.startsWith(':d2d-cli') && !it.path.startsWith(':d2d-sample')
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs files(publishedProjects.sourceSets.main.allSource.srcDirs)
    // sourceDirectories files(publishedProjects.sourceSets.main.allSource.srcDirs)
    // classDirectories files(publishedProjects.sourceSets.main.output)
    // executionData files(publishedProjects.jacocoTestReport.executionData)
    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }
    onlyIf = {
        true
    }
    doFirst {
        executionData = files(executionData.findAll {
            it.exists()
        })
    }
}

coveralls {
    sourceDirs = publishedProjects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

project(":d2d-cli") {
    apply plugin:'application'

    mainClassName = "com.megatome.d2d.Main"

    dependencies {
        implementation project(":d2d-api")
        implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    }

    test {
        jacoco {
            excludes = ["com.megatome.d2d.Main"]
        }
    }
}

project(":d2d-gradle") {
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: "com.gradle.plugin-publish"

    archivesBaseName = 'doc2dash-plugin'

    dependencies {
        implementation gradleApi()
        implementation localGroovy()
        implementation project(":d2d-api")

        testImplementation 'junit:junit:4.13.2'
        testImplementation( "org.spockframework:spock-core:2.3-groovy-4.0" ) {
            exclude group: "org.codehaus.groovy"
        }
    }

    publishing {
        repositories {
            maven {
                url = layout.buildDirectory.dir('repo')
            }
        }
    }

    gradlePlugin {
        website = "$projectLocation"
        vcsUrl = "$projectLocation"
        description = 'Create Dash docsets from Javadoc and HTML'

        plugins {
            doc2dashPlugin {
                id = 'com.megatome.doc2dash'
                displayName = 'Javadoc and HTML to Dash'
                implementationClass = 'groovy.com.megatome.Doc2DashPlugin'
                tags.set(['javadoc', 'dash', 'jsdoc', 'documentation', 'html', 'api'])
            }
        }
    }
}

project(":d2d-api") {
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    archivesBaseName = 'doc2dash-api'

    dependencies {
        implementation 'commons-io:commons-io:2.12.0'
        implementation 'org.apache.commons:commons-lang3:3.12.0'
        implementation 'org.xerial:sqlite-jdbc:3.42.0.0'
        implementation 'org.jsoup:jsoup:1.16.1'
        implementation "org.slf4j:slf4j-api:1.7.12"
        implementation "org.slf4j:slf4j-simple:1.7.12"
        implementation localGroovy()

        testImplementation 'junit:junit:4.13.2'
        testImplementation 'org.hamcrest:hamcrest-all:1.3'
        testImplementation 'com.googlecode.plist:dd-plist:1.27'
    }

    tasks.withType(Test) { task ->
        task.dependsOn ":d2d-sample:javadoc"
        task.dependsOn ":d2d-sample:javadocSplit"
        def sampleProject = findProject(":d2d-sample")
        systemProperties 'd2d-sample-javadoc': "${sampleProject.docsDir}/javadoc"
        systemProperties 'd2d-sample-javadoc-split': "${sampleProject.docsDir}/javadocSplit"
        systemProperties 'd2d-sample-jsdoc': "${sampleProject.projectDir}/src/test/JSDoc"
    }

    publishing {
        repositories {
            maven {
                url = layout.buildDirectory.dir('repo')
            }
        }
    }

    def pomConfig = {
        scm {
            connection "scm:git:git@github.com:iamthechad/javadoc2dash.git"
            developerConnection "scm:git:git@github.com:iamthechad/javadoc2dash.git"
            url "$projectLocation"
        }
        issueManagement {
            system "Github Issue Tracker"
            url "$projectLocation/issues"
        }
        licenses {
            license {
                name "The Apache Software License, Version 2.0"
                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution "repo"
            }
        }
        developers {
            developer {
                id "iamthechad"
                name "Chad Johnston"
                email "cjohnston@megatome.com"
            }
            developer {
                id "gamma"
                name "Gerry Wei√übach"
                email "gerry.w@gammaproduction.de"
            }

        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId archivesBaseName
                artifact 'sourceJar'
                artifact 'javadocJar'

                pom.withXml {
                    def root = asNode()
                    root.appendNode('name', 'Doc to Dash API')
                    root.appendNode('url', "$projectLocation")
                    root.appendNode('description', 'API for creating Dash docsets from Javadoc and HTML')
                    root.children().last() + pomConfig
                }
            }
        }
    }

    bintray {
        user = project.hasProperty('bintray_user') ? project.bintray_user : System.getenv('bintray_user')
        key = project.hasProperty('bintray_key') ? project.bintray_key : System.getenv('bintray_key')
        publications = ['mavenJava']
        dryRun = false
        publish = false
        pkg {
            repo = 'maven'
            name = 'doc2dash-api'
            desc = 'API for creating Dash docsets from Javadoc and HTML'
            websiteUrl = "$projectLocation"
            issueTrackerUrl = "$projectLocation/issues"
            vcsUrl = projectLocation + ".git"
            licenses = ['Apache-2.0']
            labels = ['javadoc', 'jsdoc', 'documentation', 'html', 'dash', 'api']
            publicDownloadNumbers = true
            version {
                name = project.version
                vcsTag = projectVersion
            }
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier = 'sources'
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        from javadoc.destinationDir
        archiveClassifier = 'javadoc'
    }
}

project(":d2d-sample") {
    javadoc {
        options.splitIndex = false
    }

    task javadocSplit(type: Javadoc) {
        source = sourceSets.main.allJava
        destinationDir = file("${project.docsDir}/javadocSplit")
        options.splitIndex = true
    }
}